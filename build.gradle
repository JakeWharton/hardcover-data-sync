import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath libs.kotlin.gradlePlugin
		classpath libs.spotless.gradlePlugin
	}
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'application'

application {
	mainClass = 'com.jakewharton.hardcover.sync.Main'
}

dependencies {
	implementation libs.clikt
	implementation libs.okhttp.client
	implementation libs.okhttp.logging
	implementation libs.kotlinx.serialization.json.okio
}

repositories {
	mavenCentral()
}

tasks.named("distTar").configure { task ->
	task.enabled = false
}
tasks.named("assemble").configure { task ->
	task.dependsOn(tasks.named("installDist"))
}

tasks.withType(KotlinJvmCompile).configureEach { task ->
	task.compilerOptions {
		jvmTarget = JvmTarget.JVM_1_8
		freeCompilerArgs.addAll(
			'-progressive',
			'-opt-in=kotlin.io.path.ExperimentalPathApi',
			'-opt-in=kotlinx.serialization.ExperimentalSerializationApi',
		)
	}
}

tasks.withType(JavaCompile).configureEach {
	sourceCompatibility = JavaVersion.VERSION_1_8.toString()
	targetCompatibility = JavaVersion.VERSION_1_8.toString()
}

apply plugin: 'com.diffplug.spotless'
spotless {
	kotlin {
		target("src/**/*.kt")
		ktlint(libs.ktlint.get().version).editorConfigOverride([
			'ktlint_standard_filename': 'disabled',
		])
	}
}
